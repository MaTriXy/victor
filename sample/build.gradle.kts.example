/*
 * Copyright 2015 Trello, Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("com.trello:victor:1.1.0")
    }
}

plugins {
    id("com.android.application")
}

// Make sure to apply this plugin *after* the Android plugin
apply(plugin = "com.trello.victor")

val Any.extensions get() = (this as org.gradle.api.plugins.ExtensionAware).extensions

android {
    compileSdkVersion(29)

    defaultConfig {
        applicationId = "com.trello.victorsample"
        minSdkVersion(15)
        targetSdkVersion(29)
        versionCode = 1
        versionName = "1.0"

        // In case you val generateVectorDrawables: use = true
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    sourceSets {
        // Example with setting one
        named("main") {
            val svgSourceSet = this.extensions["svg"] as SourceDirectorySet
            svgSourceSet.srcDir("src/${name}/svg")
        }
    }
    // Example setting all
    sourceSets.all {
        val svgSourceSet = this.extensions["svg"] as SourceDirectorySet
        svgSourceSet.srcDir("src/${name}/svg")
    }
}
configure<com.trello.victor.VictorPluginExtension> {
    // Any assets defined in relative terms needs a base DPI specified
    svgDpi = 72

    // Do not generate these densities for SVG assets
    excludeDensities = listOf("ldpi", "xxxhdpi")

    // Set this to "true" if you want to generate Android vectors instead of PNGs
    generateVectorDrawables = false
}

dependencies {
    implementation("androidx.appcompat:appcompat:1.2.0")
}
